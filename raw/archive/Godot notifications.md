---
title: "Godot notifications"
source: "https://docs.godotengine.org/en/stable/tutorials/best_practices/godot_notifications.html"
created: 2026-06-19
description: "Every Object in Godot implements a_notification method. Its purpose is to allow the Object to respond to a variety of engine-level callbacks that may relate to it. For example, if the engine tells ..."
words: 960
tags:
  - bestPractices
  - scene
  - script
  - types
  - classes
  - object
  - refCounted
  - resource
  - node
  - notification
---
Every Object in Godot implements a [\_notification](https://docs.godotengine.org/en/stable/classes/class_object.html#class-object-private-method-notification) method. Its purpose is to allow the Object to respond to a variety of engine-level callbacks that may relate to it. For example, if the engine tells a [CanvasItem](https://docs.godotengine.org/en/stable/classes/class_canvasitem.html#class-canvasitem) to "draw", it will call `_notification(NOTIFICATION_DRAW)`.

Some of these notifications, like draw, are useful to override in scripts. So much so that Godot exposes many of them with dedicated functions:

- `_ready()`: `NOTIFICATION_READY`
- `_enter_tree()`: `NOTIFICATION_ENTER_TREE`
- `_exit_tree()`: `NOTIFICATION_EXIT_TREE`
- `_process(delta)`: `NOTIFICATION_PROCESS`
- `_physics_process(delta)`: `NOTIFICATION_PHYSICS_PROCESS`
- `_draw()`: `NOTIFICATION_DRAW`

What users might *not* realize is that notifications exist for types other than Node alone, for example:

- [Object::NOTIFICATION\_POSTINITIALIZE](https://docs.godotengine.org/en/stable/classes/class_object.html#class-object-constant-notification-postinitialize): a callback that triggers during object initialization. Not accessible to scripts.
- [Object::NOTIFICATION\_PREDELETE](https://docs.godotengine.org/en/stable/classes/class_object.html#class-object-constant-notification-predelete): a callback that triggers before the engine deletes an Object, i.e. a "destructor".

And many of the callbacks that *do* exist in Nodes don't have any dedicated methods, but are still quite useful.

- [Node::NOTIFICATION\_PARENTED](https://docs.godotengine.org/en/stable/classes/class_node.html#class-node-constant-notification-parented): a callback that triggers anytime you add a child node to another node.
- [Node::NOTIFICATION\_UNPARENTED](https://docs.godotengine.org/en/stable/classes/class_node.html#class-node-constant-notification-unparented): a callback that triggers anytime you remove a child node from another node.

The universal `_notification()` method provides access to all these custom notifications.

> [!note] Note
> Methods in the documentation labeled as "virtual" are also intended to be overridden by scripts.
> 
> A classic example is the [\_init](https://docs.godotengine.org/en/stable/classes/class_object.html#class-object-private-method-init) method in Object. While it has no `NOTIFICATION_*` equivalent, the engine still calls the method. Most languages (except C#) rely on it as a constructor.

So, when should you use each of these notifications or virtual functions?

## \_process vs. \_physics\_process vs. \*\_input

Use `_process()` when you need a framerate-dependent delta time between frames. If code that updates object data needs to update as often as possible, this is the right place. Recurring logic checks and data caching often execute here, but it comes down to how often the evaluations need to update. If they don't need to execute every frame, then implementing a Timer-timeout loop is another option.

```gdscript
# Allows for recurring operations that don't trigger script logic
# every frame (or even every fixed frame).
func _ready():
    var timer = Timer.new()
    timer.autostart = true
    timer.wait_time = 0.5
    add_child(timer)
    timer.timeout.connect(func():
        print("This block runs every 0.5 seconds")
    )
```

Use `_physics_process()` when you need a framerate-independent delta time between frames. If code needs consistent updates over time, regardless of how fast or slow time advances, this is the right place. Recurring kinematic and object transform operations should execute here.

While it is possible, to achieve the best performance, you should avoid making input checks during these callbacks. `_process()` and `_physics_process()` will trigger at every opportunity (they do not "rest" by default). In contrast, `*_input()` callbacks will trigger only on frames in which the engine has actually detected the input.

You can check for input actions within the input callbacks just the same. If you want to use delta time, you can fetch it from the related delta time methods as needed.

```gdscript
# Called every frame, even when the engine detects no input.
func _process(delta):
    if Input.is_action_just_pressed("ui_select"):
        print(delta)

# Called during every input event.
func _unhandled_input(event):
    match event.get_class():
        "InputEventKey":
            if Input.is_action_just_pressed("ui_accept"):
                print(get_process_delta_time())
```

## \_init vs. initialization vs. export

If the script initializes its own node subtree, without a scene, that code should execute in `_init()`. Other property or SceneTree-independent initializations should also run here.

> [!note] Note
> The C# equivalent to GDScript's `_init()` method is the constructor.

`_init()` triggers before `_enter_tree()` or `_ready()`, but after a script creates and initializes its properties. When instantiating a scene, property values will set up according to the following sequence:

1. **Initial value assignment:** the property is assigned its initialization value, or its default value if one is not specified. If a setter exists, it is not used.
2. `_init()` **assignment:** the property's value is replaced by any assignments made in `_init()`, triggering the setter.
3. **Exported value assignment:** an exported property's value is again replaced by any value set in the Inspector, triggering the setter.

```gdscript
# test is initialized to "one", without triggering the setter.
@export var test: String = "one":
    set(value):
        test = value + "!"

func _init():
    # Triggers the setter, changing test's value from "one" to "two!".
    test = "two"

# If you set test to "three" from the Inspector, it would trigger
# the setter, changing test's value from "two!" to "three!".
```

As a result, instantiating a script versus a scene may affect both the initialization *and* the number of times the engine calls the setter.

## \_ready vs. \_enter\_tree vs. NOTIFICATION\_PARENTED

When instantiating a scene connected to the first executed scene, Godot will instantiate nodes down the tree (making `_init()` calls) and build the tree going downwards from the root. This causes `_enter_tree()` calls to cascade down the tree. Once the tree is complete, leaf nodes call `_ready`. A node will call this method once all child nodes have finished calling theirs. This then causes a reverse cascade going up back to the tree's root.

When instantiating a script or a standalone scene, nodes are not added to the SceneTree upon creation, so no `_enter_tree()` callbacks trigger. Instead, only the `_init()` call occurs. When the scene is added to the SceneTree, the `_enter_tree()` and `_ready()` calls occur.

If you need to trigger behavior that occurs as nodes parent to another, regardless of whether it occurs as part of the main/active scene or not, you can use the [PARENTED](https://docs.godotengine.org/en/stable/classes/class_node.html#class-node-constant-notification-parented) notification. For example, here is a snippet that connects a node's method to a custom signal on the parent node without failing. Useful on data-centric nodes potentially created at runtime.

```gdscript
extends Node

var parent_cache

func connection_check():
    return parent_cache.has_user_signal("interacted_with")

func _notification(what):
    match what:
        NOTIFICATION_PARENTED:
            parent_cache = get_parent()
            if connection_check():
                parent_cache.interacted_with.connect(_on_parent_interacted_with)
        NOTIFICATION_UNPARENTED:
            if connection_check():
                parent_cache.interacted_with.disconnect(_on_parent_interacted_with)

func _on_parent_interacted_with():
    print("I'm reacting to my parent's interaction!")
```

---

## User-contributed notes

*Please read the [User-contributed notes policy](https://github.com/godotengine/godot-docs-user-notes/discussions/1) before submitting a comment.*